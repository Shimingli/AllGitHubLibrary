apply plugin: 'com.android.library'



version "1.0.0"

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "${version}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

//需添加如下内容
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
//发布到组织名称名字，必须填写
group = "com.lishiming.maven"
//发布到JCenter上的项目名字，必须填写
def libName = "rxbuslibrary"
// 版本号，下次更新是只需要更改版本号即可
version = "1.0.1"
/**  上面配置后上传至jcenter后的编译路径是这样的： compile       'com.fighter.maven:mylibs:1.0.0'  **/

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    failOnError false
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到jcenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'This is shiming sdk'
                developers {
                    developer {
                        id 'rxbuslibrary'
                        name 'lishiming'
                        email 'shiminglidemo@gmail.com'
                    }
                }
            }
        }
    }
}

//上传到jcenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        userOrg = "lishiming"
        repo = "maven"
        name = libName    //发布到JCenter上的项目名字，必须填写
        desc = 'This is a test '    //项目描述
        licenses = ["Apache-2.0"]
        publish = true
    }
}

//publish {
//    repoName="rxbuslibrary"//仓库名 我远程自己创建的仓库
//    userOrg = 'lishiming'//bintray.com用户名
//    groupId = 'com.lishiming.rxbuslibrary'//jcenter上的路径
//    artifactId = 'rxbuslibrary'//项目名称
//    publishVersion = '1.0.0'//版本号
//    desc = 'Oh hi, this is a nice description for a project, right?'
//    website = 'https://github.com/Shimingli/AllGitHubLibrary'
////    636e4efc45ad6cc33c2d60ce73c0fedda6ea463d
////    gradlew clean build bintrayUpload -PbintrayUser=shimingli  -PbintrayKey=636e4efc45ad6cc33c2d60ce73c0fedda6ea463d -PbryRun=false
//}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    api  "com.jakewharton.rxrelay2:rxrelay:2.1.0"

}
//如果你开源库中有中文注释在moudel的build.gradle加入格式
allprojects {
    repositories {
        jcenter()
    }
    //加上这些
    tasks.withType(Javadoc) {
        options{ encoding "UTF-8"
            charSet 'UTF-8'
            links "http://docs.oracle.com/javase/7/docs/api"
        }
    }
}

